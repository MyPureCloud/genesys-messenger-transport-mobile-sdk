buildscript {
    ext {
        deploymentProperties = new Properties()
        oktaProperties = new Properties()
        if (System.env.JENKINS_HOME != null) {
            // when executing on jenkins, use environment variable-injected credentials
                deploymentProperties["deploymentId"] = System.env.DEPLOYMENT_ID
                deploymentProperties["deploymentDomain"] = System.env.DEPLOYMENT_DOMAIN
            //Okta variables
            oktaProperties["oktaDomain"] = System.env.OKTA_DOMAIN
            oktaProperties["clientId"] = System.env.CLIENT_ID
            oktaProperties["signInRedirectUri"] = System.env.SIGN_IN_REDIRECT_URI
            oktaProperties["signOutRedirectUri"] = System.env.SIGN_OUT_REDIRECT_URI
            oktaProperties["oktaState"] = System.env.OKTA_STATE
            oktaProperties["codeChallenge"] = System.env.CODE_CHALLENGE
            oktaProperties["codeChallengeMethod"] = System.env.CODE_CHALLENGE_METHOD
            oktaProperties["codeVerifier"] = System.env.CODE_VERIFIER
        } else {
            // when executing elsewhere (dev laptops, etc), use the secure.properties file on disk
            deploymentPropertiesFile = new File(rootDir.absolutePath + "/deployment.properties")
            if (deploymentPropertiesFile.exists()) {
                deploymentProperties.load(deploymentPropertiesFile.newReader())
            } else {
                throw new GradleException("Deployment id and domain must be specified in either environmental variables for Jenkins or ${rootDir.absolutePath}/deployment.properties locally.")
            }
            oktaPropertiesFile = new File(rootDir.absolutePath + "/okta.properties")
            if (oktaPropertiesFile.exists()) {
                oktaProperties.load(oktaPropertiesFile.newReader())
            } else {
                logger.warn("okta.properties does not exist in the path and will be ignored.")
                 throw new GradleException("Okta configuration must be specified in ${rootDir.absolutePath}/okta.properties locally.")
            }
        }
    }
    repositories { gradlePluginPortal() }
    dependencies {
        classpath("org.jmailen.gradle:kotlinter-gradle:" + libs.versions.kotlinter.get())
    }
}

plugins {
    id 'com.android.application'
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.kotlinter)
    id 'com.google.gms.google-services'
    alias(libs.plugins.kotlin.compose)
}

android {

    android.buildFeatures.buildConfig true
    defaultConfig {
        applicationId "com.genesys.cloud.webmessaging.androidcomposeprototype"
        minSdkVersion 21
        compileSdk 35
        targetSdkVersion 35
        versionCode 1
        versionName "1.0"

        def deploymentId = ""
        if(deploymentProperties["deploymentId"] != null) {
            deploymentId = deploymentProperties["deploymentId"]
        }
        def deploymentDomain = "inintca.com"
        if(deploymentProperties["deploymentDomain"] != null) {
            deploymentDomain = deploymentProperties["deploymentDomain"]
        }
        buildConfigField "String", "DEPLOYMENT_ID", '"' + deploymentId + '"'
        buildConfigField "String", "DEPLOYMENT_DOMAIN", '"' + deploymentDomain + '"'
        // OKTA
        buildConfigField "String", "OKTA_DOMAIN", '"' + oktaProperties["oktaDomain"] + '"'
        buildConfigField "String", "CLIENT_ID", '"' + oktaProperties["clientId"] + '"'
        buildConfigField "String", "SIGN_IN_REDIRECT_URI", '"' + oktaProperties["signInRedirectUri"] + '"'
        buildConfigField "String", "SIGN_OUT_REDIRECT_URI", '"' + oktaProperties["signOutRedirectUri"] + '"'
        buildConfigField "String", "OKTA_STATE", '"' + oktaProperties["oktaState"] + '"'
        buildConfigField "String", 'CODE_CHALLENGE', "\"${oktaProperties.getProperty('codeChallenge')}\""
        buildConfigField "String", 'CODE_CHALLENGE_METHOD', "\"${oktaProperties.getProperty('codeChallengeMethod')}\""
        buildConfigField "String", 'CODE_VERIFIER', "\"${oktaProperties.getProperty('codeVerifier')}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        compose true
    }
    sourceSets {
        main {
            assets {
                srcDirs 'src/main/assets', 'src/androidTest/assets'
            }
        }
    }
    namespace 'com.genesys.cloud.messenger.androidcomposeprototype'
}

dependencies {
    implementation(project(':transport'))

    // Required by minSdk 21 with Java 17
    coreLibraryDesugaring(libs.desugar.jdk.libs)

    implementation libs.core.ktx
    implementation libs.appcompat
    implementation libs.material
    implementation libs.lifecycle.runtime.ktx
    implementation libs.fragment.ktx

    // Compose
    implementation platform(libs.compose.bom)
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.material:material-icons-core-android'
    implementation 'androidx.compose.ui:ui-tooling'
    implementation 'androidx.compose.foundation:foundation'
    implementation libs.lifecycle.viewmodel.compose
    implementation libs.activity.compose

    // Chrome Custom Tabs
    implementation libs.browser
    implementation libs.firebase.messaging

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.android.device.names
    androidTestImplementation libs.awaitility.kotlin
    androidTestImplementation libs.uiautomator
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.jackson.module.kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    configurations.all {
        resolutionStrategy.dependencySubstitution {
            substitute(module("org.hamcrest:hamcrest-library:1.3")).using(module("junit:junit:" + libs.versions.junit.get()))
            substitute(module("org.hamcrest:hamcrest-core:1.3")).using(module("junit:junit:" + libs.versions.junit.get()))
        }
    }
}

afterEvaluate {
    tasks.withType(org.jmailen.gradle.kotlinter.tasks.LintTask).configureEach {
        exclude('**/BuildKonfig.kt')
    }
}

repositories {
    mavenCentral()
}
